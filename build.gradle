plugins {
    id 'fml-loom' version "0.1.local"
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.0"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    mavenLocal() // for local files
    maven {
        name = "Spongepowered"
        url = "https://repo.spongepowered.org/repository/maven-public/"
    }//Dependency library(FastUtil, Gson, etc) download source
}

configurations {
    implementation.extendsFrom shadow
}

loom {
    accessWidenerPath = file("src/main/resources/miteatmos.accesswidener")
    mergedMinecraftJar()
    setFML(new File("libs\\FishModLoader-v${loader_version}.jar"))
    mods {
        "modid" {
            sourceSet sourceSets.main
//            sourceSet sourceSets.client
//            sourceSet sourceSets.server
            //For modules in the ’src‘ directory, only the main module is required by default
        }
    }
}

runClient {
    args("--username", "${user_name}", "--session", "a.a.a.a", "--width", "${window_width}", "--height", "${window_height}")
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.fmlMCPMappings()
    implementation files(loom.getFML().toPath())
    implementation("it.unimi.dsi:fastutil:8.5.12")
    implementation("com.google.code.gson:gson:2.11.0")

    shadow(implementation "net.sf.practicalxml:practicalxml:1.1.18")
    shadow(implementation "net.sf.kdgcommons:kdgcommons:1.0.14")
    include "net.sf.practicalxml:practicalxml:1.1.18"
    include "net.sf.kdgcommons:kdgcommons:1.0.14"
}

shadowJar {
    configurations = [project.configurations.shadow]
    exclude("fabric.*")
    mergeServiceFiles()
    archiveFileName = "${archives_base_name} [${version}].jar"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fml.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    enabled = false
    dependsOn(shadowJar)
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}